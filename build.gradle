plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'jacoco'
    id 'com.gradle.build-scan' version '1.16'
//    id 'com.gradle.build-scan' version '3.3.4'
    id 'maven-publish'
    id 'signing'
}

ext {
    publishBuildScan = false
    snapshotVersion = true
    jacksonVersion = '2.11.0'
}

group 'com.nfeld.jsonpathlite'
version = '2.0.0' + (snapshotVersion ? "-SNAPSHOT" : "")

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

apply from: "tests_codecov.gradle"
apply from: "publish.gradle"

buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"

    if (publishBuildScan) {
        publishAlways()
    }
}